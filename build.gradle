plugins{
  id 'java-library'
  id 'net.kyori.indra.publishing' version '3.1.3'
}

group = 'com.xpdustry'
version = 'v152.2'

repositories{
    mavenCentral()
    maven{ url 'https://jitpack.io' }
}

dependencies{
    testImplementation 'junit:junit:4.11'
    // Optional dependencies. See README
    compileOnly "org.fusesource.jansi:jansi-native:1.1"
    compileOnly "org.fusesource.jansi:jansi-native:1.1:windows32"
    compileOnly "org.fusesource.jansi:jansi-native:1.1:windows64"
}

tasks.withType(JavaCompile){
    java.sourceCompatibility = 1.8
    java.targetCompatibility = 1.8
    options.compilerArgs.addAll(['--release', '8'])
    options.encoding = 'UTF-8'
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

java{
    withJavadocJar()
    withSourcesJar()
}

jar.archiveFileName = rootProject.name + '.jar'

// Publishing
signing{
    def signingKey = findProperty('signingKey')
    def signingPassword = findProperty('signingPassword')
    useInMemoryPgpKeys(signingKey, signingPassword)
}

indra{
    javaVersions{
        target(8)
        minimumToolchain(8)
    }
      
    apache2License()
    publishReleasesTo('xpdustry', 'https://maven.xpdustry.com/releases')
  
    github('xpdustry', rootProject.name){
        ci(true)
        issues(true)
        scm(true)
    }
  
    configurePublications{
        from components.java
    
        pom{
            organization{
                name = 'Xpdustry'
                url = 'https://www.xpdustry.com'
            }
      
            developers{
                developer{
                  id = 'Anuken'
                  url = 'https://github.com/Anuken'
                }
                
                developer{
                  id = 'ZetaMap'
                  url = 'https://github.com/ZetaMap'
                }
            }
        }
    }
}
